class RT_Monster_AIHelper : Inventory
{
	float timeSinceLastSeen;

	default {
		Inventory.MaxAmount 1;
		+INVENTORY.PERSISTENTPOWER;
	}

	bool SeekPlayer(bool hasSeenPlayer) {
		FLineTraceData testNode;
		int offset = owner.angle + random(-30, 30);
		int vangle = random(-15, 15);
		owner.LineTrace(offset, 2048, vangle, TRF_ALLACTORS, owner.height - owner.height / 3, data: testNode);

		PlayerInfo p = players[consoleplayer];
		bool debug_enabled = Cvar.GetCVar("rt_debug", p).GetBool();
		if (debug_enabled) {
			for (int i = 0; i < 5; i++) {
				Vector3 pos = owner.Pos;
				pos.x = pos.x + Cos(offset) * 20 * i;
				pos.y = pos.y + Sin(offset) * 20 * i;
				pos.z = pos.z + (owner.height - (owner.height / 3)) + Tan(vangle) * 20 * i;
				if (!hasSeenPlayer) {
					owner.Spawn("RTDebugParticle", pos, NO_REPLACE);
				} else {
					owner.Spawn("RTDebugParticle2", pos, NO_REPLACE);
				}
			}
		}

		if (testNode.HitType == TRACE_HitActor && testNode.HitActor.player)
		{
			if (!testNode.HitActor.CheckInventory("PowerInvisibility", 1)) {
				// A_LOG("Got you!");
				return true;
			} else {
				// A_LOG("Thermoptic saved you!");
				return false;
			}
		}

		return false;
	}

	override void DoEffect() {
		super.DoEffect();
		if (!owner) {
			return;
		}
		
		if (InStateSequence(owner.CurState, owner.ResolveState("Idle")) || InStateSequence(owner.CurState, owner.ResolveState("Wander"))) {
			bool foundPlayer = SeekPlayer(false);
			if (foundPlayer) {
				owner.SetState(owner.ResolveState("See"));
				timeSinceLastSeen = 0.0;
			}
		} else if (InStateSequence(owner.CurState, owner.ResolveState("See"))) {
			timeSinceLastSeen += 1.0 / TICRATE;
			bool foundPlayer = SeekPlayer(true);
			if (foundPlayer) {
				timeSinceLastSeen = 0.0;
			} else {
				if (timeSinceLastSeen > 8) {
					// A_LOG("Where did they go...?");
					owner.SetState(owner.ResolveState("Wander"));
				}
			}
		}
	}


	override void PostBeginPlay()
	{
		timeSinceLastSeen = 0.0;
	}
}