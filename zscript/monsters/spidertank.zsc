class SpiderTankRT: Actor replaces SpiderMastermind {
    float strafe_fire_i;
    Default {
        Monster;
        +NOINFIGHTING;
        SeeSound "";
        AttackSound "grunt/attack";
        PainSound "";
        DeathSound "";
        ActiveSound "";
        Speed 0;
        Health 1250;
        Radius 128;
        Height 100;
        Mass 1000;
        PainChance 256;
        MinMissileChance 160;
        +DONTMORPH;
        Obituary "%o was destroyed by a Spider Tank.";
        Tag "Spider Tank";
    }

    override void PostBeginPlay()
    {
        strafe_fire_i = 30;
    }

    States {
        Spawn:
            Goto Idle;
        Idle:
            STNK A 10;
            Loop;
        See:
            STNK A 3 A_Metal;
            STNK AAA 3 A_Chase; // ABB
            STNK A 3 A_Metal; // C
            STNK AAA 3 A_Chase; // CCD
            STNK A 3 A_Metal; // E
            STNK AAA 3 A_Chase; // EFF
            Loop;
        ShootSmoke:
            STNK A 30 Bright;
            STNK A 0 Bright  A_CustomMissile("SmokeGrenadeProjectile", 120, 0, -90, CMF_AIMDIRECTION , -20); // G
            STNK A 0 Bright  A_CustomMissile("SmokeGrenadeProjectile", 120, 0, 0, CMF_AIMDIRECTION , -20); // G
            STNK A 0 Bright  A_CustomMissile("SmokeGrenadeProjectile", 120, 0, 90, CMF_AIMDIRECTION , -20); // G
            STNK A 30 Bright;
            Goto See;
        Missile:
            STNK A 20 Bright;
            STNK A 0 Bright { // G
                if (!target.CheckInventory("PowerInvisibility", 1)) {
                    A_FaceTarget();
                } else {
                    // A_LOG("Can't see you!");
                }
                strafe_fire_i -= 3;
                if (strafe_fire_i < -30) {
                    strafe_fire_i = 30;
                    SetState(ResolveState("ShootSmoke"));
                }
            }
            STNK A 0 Bright A_PlaySound("spider/attack", CHAN_WEAPON);  // G
            STNK A 2 Bright A_CustomBulletAttack(strafe_fire_i, random(-1,1), 1, random(1,5)*3, "BulletPuff", 0, CBAF_NORANDOM | CBAF_AIMFACING | CBAF_EXPLICITANGLE, AAPTR_TARGET, "RTWeaponTracer", 70);  // G
            STNK A 0 Bright A_PlaySound("spider/attack", CHAN_WEAPON); // G
            STNK A 2 Bright A_CustomBulletAttack(strafe_fire_i, random(-1,1), 1, random(1,5)*3, "BulletPuff", 0, CBAF_NORANDOM | CBAF_AIMFACING | CBAF_EXPLICITANGLE, AAPTR_TARGET, "RTWeaponTracer", 70); // H
            Goto Missile+1;
        Pain:
            STNK A 3; // I
            STNK A 3 A_Pain; // I
            Goto See;
        Death:
            STNK A 20 A_Scream; // J
            STNK A 10 A_NoBlocking; // K
            STNK AAAAAAA 10; // LMNOPQR
            STNK A 30; // S
            STNK A -1 A_BossDeath; // S
            Stop;
    }
}