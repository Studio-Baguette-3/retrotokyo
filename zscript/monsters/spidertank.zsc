class SpiderTankRT: SpiderMastermind replaces SpiderMastermind {
    float strafe_fire_i;
    Default {
        +NOINFIGHTING;
        Translation "112:119=200:207", "120:127=240:247";
        SeeSound "";
        AttackSound "grunt/attack";
        PainSound "player/male/pain50_1";
        DeathSound "player/male/death2";
        ActiveSound "";
        Speed 0;
    }

    override void PostBeginPlay()
    {
        strafe_fire_i = 30;
    }

    States {
        Spawn:
            SPID AB 10 A_Look;
            Loop;
        See:
            SPID A 3 A_Metal;
            SPID ABB 3 A_Chase;
            SPID C 3 A_Metal;
            SPID CDD 3 A_Chase;
            SPID E 3 A_Metal;
            SPID EFF 3 A_Chase;
            Loop;
        ShootSmoke:
            SPID A 30 Bright;
            SPID G 0 Bright  A_CustomMissile("SmokeGrenadeProjectile", 48, 0, -90);
            SPID G 0 Bright  A_CustomMissile("SmokeGrenadeProjectile", 48, 0, 0);
            SPID G 0 Bright  A_CustomMissile("SmokeGrenadeProjectile", 48, 0, 90);
            SPID A 30 Bright;
            Goto See;
        Missile:
            SPID A 20 Bright;
            SPID G 0 Bright {
                A_FaceTarget();
                strafe_fire_i -= 3;
                if (strafe_fire_i < -30) {
                    strafe_fire_i = 30;
                    SetState(ResolveState("ShootSmoke"));
                }
            }
            SPID G 0 Bright A_PlaySound("spider/attack", CHAN_WEAPON);
            SPID G 2 Bright A_CustomBulletAttack(strafe_fire_i, random(-1,1), 1, random(1,5)*3, "BulletPuff", 0, CBAF_NORANDOM | CBAF_AIMFACING | CBAF_EXPLICITANGLE, AAPTR_TARGET, "RTWeaponTracer");
            SPID G 0 Bright A_PlaySound("spider/attack", CHAN_WEAPON);
            SPID H 2 Bright A_CustomBulletAttack(strafe_fire_i, random(-1,1), 1, random(1,5)*3, "BulletPuff", 0, CBAF_NORANDOM | CBAF_AIMFACING | CBAF_EXPLICITANGLE, AAPTR_TARGET, "RTWeaponTracer");
            Goto Missile+1;
        Pain:
            SPID I 3;
            SPID I 3 A_Pain;
            Goto See;
        Death:
            SPID J 20 A_Scream;
            SPID K 10 A_NoBlocking;
            SPID LMNOPQR 10;
            SPID S 30;
            SPID S -1 A_BossDeath;
            Stop;
    }
}