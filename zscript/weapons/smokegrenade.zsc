Class SmokeGrenade : Ammo
{
  Default {
    Inventory.PickupMessage "Smoke grenades";
    Inventory.Amount 2;
    Inventory.MaxAmount 4;
    Inventory.Icon "CLIPA0";
    Ammo.BackpackAmount 2;
    Ammo.BackpackMaxAmount 4;
  }

  States
  {
    Spawn:
      CLIP A -1;
      stop;
  }
}

Class RTSmoke : Actor {
  Default {
    Radius 60;
    Height 60;
    +NOGRAVITY;
    +NOCLIP;
    +DONTSPLASH;
    +NOTELEPORT;
    Gravity 0.0;
    Scale 2;
  }
  States
  {
    Spawn:
      RTR0 A 1000 BRIGHT; // TODO: multiple frames!
      stop;
  }

  override void Tick() {
    PlayerInfo p = players[consoleplayer];
    bool b = Cvar.GetCVar("rt_visionmode", p).GetBool();
    if (!b) {
      self.A_SetRenderStyle(1.0, STYLE_TRANSLUCENT);
    } else {
      if (p.cls == "Support") {
        self.A_SetRenderStyle(0.03, STYLE_TRANSLUCENT);
      }
    }
    return Super.Tick();
  }
}

Class SmokeGrenadeProjectile : Grenade
{
  Default {
    Gravity 0.5;
    BounceFactor 0.3;
    WallBounceFactor 0.3;
  }

  States
  {
    Death:
      TNT1 A 1 {
        for(int i = 0; i < 25; i++)
        {
          Vector3 pos = self.Pos;
          pos.x = pos.x + Random(-50, 50);
          pos.y = pos.y + Random(-50, 50);
          pos.z = pos.z + Random(-50, 50);
          pos.z = pos.z + 25;
          Spawn("RTSmoke", pos, NO_REPLACE);
        }
      }
      Stop;
  }
}

Class SmokeGrenadeHand : Pistol
{
  Default {
    Weapon.SlotNumber 2;
    Weapon.AmmoType "SmokeGrenade";
    Weapon.AmmoGive 20;
    +Weapon.NoAlert;
  }

  States
  {
    Ready:
      PUNG A 1 A_WeaponReady;
      Loop;
    Deselect:
      PUNG A 1 A_Lower;
      Loop;
    Select:
      PUNG A 1 A_Raise;
      Loop;
    Fire:
      PUNG A 1;
      PUNG B 1 A_FireCustomMissile("SmokeGrenadeProjectile");
      PUNG A 1 A_CheckReload;
      PUNG A 1 A_Refire;
      Goto Ready;
  }
}